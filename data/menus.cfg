///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
//  Standard menu definitions:
//  (Don't modify, add personal menus to 'autoexec.cfg' instead.)
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
exec "data/maplists.cfg"
exec "data/menus_utility.cfg"
exec "data/menus_texteditor.cfg"

exec "data/menus_editing.cfg"
exec "data/menus_managers.cfg"
exec "data/menus_master.cfg"
exec "data/menus_options.cfg"


///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
togglemainmenu = [ || (cleartexgui) [cleargui 1] [showgui "main"] ]


//  GAME:
adjustgameclock   = [showgui "adjustgameclock"]
adjusthudscore    = [showgui "adjusthudscore"]
adjusthud         = [showgui "adjusthud"]
chooseplayermodel = [guirolloveraction = (PM_button $playermodel); showgui "playermodel"]
master            = [showgui "master"]
showcustommaps    = [showgui "custommaps"]


//  EDIT:
mapfog            = [if (|| [= $getmode 1] [! (ingamemp)]) [showgui "map_fog"]]
maplighting       = [if (|| [= $getmode 1] [! (ingamemp)]) [showgui "map_lighting"]]
mapmaterials      = [if (|| [= $getmode 1] [! (ingamemp)]) [showgui "map_materials"]]
mapsky_atmo       = [if (|| [= $getmode 1] [! (ingamemp)]) [showgui "map_sky_atmo"]]
mapsky_cloudbox   = [if (|| [= $getmode 1] [! (ingamemp)]) [showgui "map_sky_cloudbox"]]
mapsky_cloudlayer = [if (|| [= $getmode 1] [! (ingamemp)]) [showgui "map_sky_cloudlayer"]]
mapsky_skybox     = [if (|| [= $getmode 1] [! (ingamemp)]) [showgui "map_sky_skybox"]]

mapvars = [if (|| [= $getmode 1] [! (ingamemp)]) [showgui "map_vars"]]

toggleentgui = [if $editing [if (|| [cleargui] [cleartexgui]) [] [showentgui]]]
togglemmgui  = [if $editing [if (|| [cleargui] [cleartexgui]) [] [showgui "mapmodels"]]]
togglepfgui  = [if $editing [if (|| [cleargui] [cleartexgui]) [] [showgui "prefabs"]]]
toggletexgui = [if $editing [if (|| [cleargui] [cleartexgui]) [] [showtexgui]]]
toggleentman = [if $editing [if (|| [cleargui] [cleartexgui]) [] [showgui "entitymanager"]]]
toggletexman = [if $editing [if (|| [cleargui] [cleartexgui]) [] [showgui "texslotmanager"]]]

vcolorgui       = [if $editing [if (|| [cleargui] [cleartexgui]) [] [showgui "vcolor"]]]
vshaderparamgui = [if $editing [if (|| [cleargui] [cleartexgui]) [] [showgui "vshaderparam"]]]


//  MISC:
notepad     = [if (> $numargs 0) [notepadfile = $arg1]; showgui "notepad"]
pastebuffer = [showgui "pastebuffer"]
scratchpad  = [showgui "scratchpad"]

pakman = [showgui "packagemanager"]

postfx = [showgui "postfx"]


//  TEST:
guitest_font    = [showgui "testfont"]
guitest_padding = [showgui "testpadding"]


///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
nodebug [
	defvarp mainmenu_showlanconnect    0 0 1
	defvarp mainmenu_showconnectlocal  0 0 1
	defvarp mainmenu_showbotmatch      0 1 1
	defvarp mainmenu_shownewmap        0 0 1
	defvarp mainmenu_showloadmap       0 1 1
	defvarp mainmenu_showgetdemo       0 0 1
	defvarp mainmenu_showloaddemo      0 0 1
]

newgui "main" [
	guilist [// H
		guiimage (concatword "packages/icons/" (playermodelicon) ".png") [chooseplayermodel] 1.15
		guistrut 0.25
		guilist [// V
			MyName = $getname
			if $offline [
				guifield MyName 15 [name $MyName]
			][
				guitext $MyName
			]
			guispring
			guilist [// H
				guibutton (playermodelname) [chooseplayermodel] 0
				guistrut 1
				guiimage (getcrosshair) [showgui "crosshair"] 0.5
				guistrut 0.5
				if $offline [
					guibutton (concatword "^f3" "OFFLINE") [offline 0] 0
				][
					guibutton (concatword "^f0" "ONLINE") [offline 1] 0
				]
			]
		]
	]
	guibar
	guibutton "server browser" [showgui "serverbrowser"]
	if (|| $mainmenu_showlanconnect $mainmenu_showconnectlocal) [guistrut 0.333]
	if $mainmenu_showlanconnect   [guibutton "lanconnect"]
	if $mainmenu_showconnectlocal [guibutton "connect localhost"]
	guibar
	if (ingame) [
		if (|| $editing [= $getmode 1]) [guibutton "t:gui.main.editing"    [showgui "editing"]; guistrut 0.333]
		if (m_teammode $getmode)        [guibutton "t:gui.main.switchteam" [switchteam]]
										 guibutton "t:gui.main.togglespec" [togglespectator]   "spectator"
		if (ingamemp)                   [guibutton "t:gui.main.moptions"   [showgui "master"]]
		guistrut 0.333
	]
	if (ingamemp) [
		if $mainmenu_showgetdemo  [guibutton "get demo"  [getdemo]        ; guistrut 0.333]
	][  if $mainmenu_showloaddemo [guibutton "load demo" [showgui "demos"]; guistrut 0.333]
	]
	if (! (ingame)) [
		if $mainmenu_showbotmatch [guibutton "t:gui.main.botmatch" [showgui "botmatch"]]
	]
	if (&& $mainmenu_shownewmap [|| [! (ingame)] [&& (ingame) $editing]]) [
		guibutton "new map" [newmap]
	]
	if $mainmenu_showloadmap       [guibutton "load map"        [showgui "maps"]]
	guibar
	guibutton "t:gui.main.options" [showgui "options"]
	if (ingame) [
		guibutton "t:gui.main.disconnect" [disconnect] "exit"
	][
		guibutton "t:gui.main.about" [showgui "about"]
		guibutton "t:gui.main.quit"  [quit] "exit"
	]
] 0


///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
nodebug [
	defvar playermodelcount 1 6 0
]

macro PM_button [cleargui 1; playermodel %1]

newgui "playermodel" [
	guilist [// H
		guilist [// V
			loop iMdl $playermodelcount [
				guibutton (playermodelname $iMdl) (PM_button $iMdl) (playermodelicon $iMdl)
			]
		]
		guibar
		iMdl = (substr $guirolloveraction (strlen (PM_button "")))
		iMdl = (max 0 (min (- $playermodelcount 1) $iMdl))
		guilist [// V
			guilist [// H
				guispring
				guiplayerpreview $iMdl 0 6 (checkrolloveraction "playermodel ") 4 1 (playermodelname $iMdl)
				guispring
			]
			guitextbox (playermodelstory $iMdl) 40 9
		]
	]
	guitab "t:gui.player.setup"

	guilist [
		guiradio "t:gui.player.fp" thirdperson 0 "thirdperson 0"
		guibar
		guiradio "t:gui.player.tp" thirdperson 1 "thirdperson 1"
	]
	if $thirdperson [
		guitext "t:sgui.player.tp.dist"
		guislider thirdpersondistance 10 30
		guitext "t:gui.player.tp.up"
		guislider thirdpersonup -10 10
		guitext "t:gui.player.tp.side"
		guislider thirdpersonside -10 10
	]
	guibar
	guilist [
		guibutton "t:gui.player.color.rifle" "showgui rifletrail"
		guibar
		guibutton "t:gui.player.color.blood" "showgui bloodcolor"
	]
] "t:gui.player.pm"


///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
newgui "serverbrowser" [
	guistayopen [
		guiservers [
			guialign 0 [
				guibutton   "t:gui.servers.update"  updatefrommaster
				guistrut 4
				newfilterdesc = $filterservers
				//guitext (contactword (gettrans "gui.servers.search") " "); guifield newfilterdesc 10 [filterservers $newfilterdesc]; guibutton "" [filterservers ""] "exit"
				//guistrut 4
				guicheckbox (concatword (? $autoupdateservers "" "^f9") (gettrans "gui.servers.autoupdate"))  autoupdateservers
				guistrut 4
				guicheckbox (concatword (? $searchlan         "" "^f9") (gettrans "gui.servers.lan"))  searchlan
				guistrut 4
				guicheckbox (concatword (? $autosortservers   "" "^f9") (gettrans "gui.servers.autosort"))  autosortservers
				if (! $autosortservers) [
					guistrut 2
					guibutton "t:gui.servers.sort" sortservers
				]
			]
			guibar
		] 17
	]
] "t:gui.servers" [initservers]


///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
nodebug [
	defvar BM_BotCount 1   5  32
	defvar BM_MinSkill 1  50 101
	defvar BM_MaxSkill 1 100 101
]

BM_StartMatch = [
	if (ingamemp) [
		echo (gettrans "gui.botmatch.disconnect")
	] [
		_steam_botmatch // TODO: do this better
		loop i $BM_BotCount [addbot (rnd (+ $BM_MaxSkill 1) $BM_MinSkill)]
	]
]
newgui "botmatch" [
	guitext "t:gui.botmatch.amount"
	guislider BM_BotCount 0 32
	guitext "t:gui.botmatch.min"
	guislider BM_MinSkill 50 101 [if (< $BM_MaxSkill $BM_MinSkill) [BM_MaxSkill = $BM_MinSkill]]
	guitext "t:gui.botmatch.max"
	guislider BM_MaxSkill 50 101 [if (> $BM_MinSkill $BM_MaxSkill) [BM_MinSkill = $BM_MaxSkill]]
	guibutton "t:gui.botmatch.start" [guionclear [BM_StartMatch]; showgui "maps"]
] "t:gui.botmatch"


///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
MapList_Custom = "" //  Alias strings can be much longer.

newgui "maps" [
	guikeeptab 1
	ML_CurrentTab 0 //<-- This is gross.
	
	case ($ML_ModeMaps) 0 [
		guimaplist "t:gui.map"         $MapList_DM      15 17 2
		guimaplist "t:gui.map.ctf"     $MapList_CTF     15 17 2
		guimaplist "t:gui.map.parkour" $MapList_Parkour 15 17 2
	] 1 [
		guimaplist "t:gui.map.capture" $MapList_Capture 15 17 2
	] 2 [
		guimaplist "t:gui.map.ctf"     $MapList_CTF     15 17 2
	] 3 [
		guimaplist "t:gui.map.race"    $MapList_Race    15 17 2
	]

	guimaplist "t:gui.map.custom"  $MapList_Custom  15 17 2
] "t:gui.map" [ML_MapName ""; GenMapList_Custom]

GenMapList_Custom = [
	MapList_Custom = ""
	loopfiles File "packages/maps" "cmr" [
		if (< (indexof $MapList_ALL $File) 0) [
			MapList_Custom = (concat $MapList_Custom (escape $File))
		]
	]
	MapList_Custom = (sortlist $MapList_Custom iA iB [naturalsort $iA $iB])
]

///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
DemoList = "" //  Alias strings can be much longer.

newgui "demos" [
	guikeeptab 1
	looplist Demo $DemoList [
		guibutton $Demo (concat demo $Demo)
	]
] "" [GenDemoList]

GenDemoList = [
	DemoList = ""
	loopfiles File $demodir "cdm" [
		DemoList = (concatword $DemoList (escape $File))
	]
	DemoList = (sortlist $DemoList iA iB [naturalsort $iA $iB])
]

///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////

newgui "about" [
	guititle "^fhCarmine Impact"
	guialign 0 [guitext "^f4by"]
	guialign 0 [guitext "^f7Zsolt ^"^f2yellowberry^f7^" Zitting"]
	guialign 0 [guitext "^f7Jacob ^"^f2Bounty.jpeg^f7^" Peterson"]
	guialign 0 [guitext "^f7Joseph ^"^f2NoobGuy^f7^" Smythe"]
	guialign 0 [guitext "^f7Kristen ^"^f2Cynthetic^f7^""]
	guialign 0 [guitext "^f7& others"]
	guistrut 0.5
	guialign 0 [guitext "^f4In loving memory of Marvin Christensen Zitting."]
	guistrut 0.5
	guialign 0 [guitext "^f7For a full list of contributors see the ^f2readme^f7."]
	guistrut 0.5
	guialign 0 [guitext "^f2https://harpnetstudios.com/games/carmine"]
	
	guitab "Cube 2"
	guititle "Cube 2: Sauerbraten"
	guialign 0 [guitext "^f4by"]
	guialign 0 [guitext "^f7Wouter ^"^f2Aardappel^f7^" van Oortmerssen"]
	guialign 0 [guitext "^f7Lee ^"^f2eihrul^f7^" Salzman"]
	guialign 0 [guitext "^f7Mike ^"^f2Gilt^f7^" Dysart"]
	guialign 0 [guitext "^f7Robert ^"^f2baby-rabbit^f7^" Pointon"]
	guialign 0 [guitext "^f7John ^"^f2geartrooper^f7^" Siar"]
	guialign 0 [guitext "^f7Quinton ^"^f2Quin^f7^" Reeves"]
	guialign 0 [guitext "^f7& others"]
	guistrut 0.5
	guialign 0 [guitext "^f7For a full list of contributors see the ^f2readme^f7."]
	guistrut 0.5
	guialign 0 [guitext "^f2http://sauerbraten.org/"]
] "Carmine Impact"

///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////

nodebug [
	defvar RifleTrailR 0 0 255
	defvar RifleTrailG 0 0 255
	defvar RifleTrailB 0 0 255
]

newgui "rifletrail" [
	guilist [// H
		guilist [// V
			guistrut 14 1
			guialign 1 [guitext "Color: "]
			guistrut 1
			guialign 1 [guicolor $rifletrail; guistrut 1]
		]
		guilist [// V
			guistrut 56 1
			RifleTrailR (& (>> $rifletrail 16) 0xFF)
			RifleTrailG (& (>> $rifletrail  8) 0xFF)
			RifleTrailB (&     $rifletrail     0xFF)
			guislider RifleTrailR 0 255 [rifletrail $RifleTrailR $RifleTrailG $RifleTrailB]
			guislider RifleTrailG 0 255 [rifletrail $RifleTrailR $RifleTrailG $RifleTrailB]
			guislider RifleTrailB 0 255 [rifletrail $RifleTrailR $RifleTrailG $RifleTrailB]
		]
	]
] "t:gui.rifletrail"

nodebug [
	defvar BloodColorR 0 0 255
	defvar BloodColorG 0 0 255
	defvar BloodColorB 0 0 255
]

newgui "bloodcolor" [
	guilist [// H
		guilist [// V
			guistrut 14 1
			guialign 1 [guitext "Color: "]
			guistrut 1
			guialign 1 [guicolor $bloodcolor; guistrut 1]
		]
		guilist [// V
			guistrut 56 1
			BloodColorR (& (>> $bloodcolor 16) 0xFF)
			BloodColorG (& (>> $bloodcolor  8) 0xFF)
			BloodColorB (&     $bloodcolor     0xFF)
			guislider BloodColorR 0 255 [bloodcolor $BloodColorR $BloodColorG $BloodColorB]
			guislider BloodColorG 0 255 [bloodcolor $BloodColorR $BloodColorG $BloodColorB]
			guislider BloodColorB 0 255 [bloodcolor $BloodColorR $BloodColorG $BloodColorB]
		]
	]
] "t:gui.bloodcolor"
