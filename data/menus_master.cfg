///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
nodebug [
    defvarp mastermenu_confirmkick   0 1 1
    defvarp mastermenu_showbots      0 0 1
    defvarp mastermenu_showself      0 1 1
    defvarp mastermenu_splitplyrspec 0 1 1

    defsvar SM_ServDesc   ""
    defsvar SM_ServAddr   ""

    defvar  SM_NumOfClnt  0 0 256
    defvar  SM_NumOfSpec  0 0 256
    defvar  SM_NumOfPlyr  0 0 256
    defvar  SM_NumOfBots  0 0 256
    defvar  SM_MasterMode 0 0   3

    defvar  SM_ClntIsMstr 0 0 1
    defvar  SM_ClntIsSpec 0 0 1

    defvar  SM_KickMe -1 -1 256

    SM_ListClnt = ""
    SM_ListAdmn = ""
    SM_ListAuth = ""
    SM_ListMstr = ""
    SM_ListBots = ""
    SM_ListPlyr = ""
    SM_ListSpec = ""
]

newgui "master" [
    guikeeptab 1
    guistayopen [guinoautotab [
    guilist [
        guistrut (max $SM_NumOfSpec (max $SM_NumOfPlyr $SM_NumOfBots)) 1
        guilist [
            SM_Update
            guialign 0 [guititle $SM_ServDesc]
            guialign 0 [guitext  $SM_ServAddr]
            guibar //-------------------------------------------------------------------------
            guialign 0 [//H
                SM_MasterMode $getmastermode
                guitext (concatword "^f7mastermode ^f2" $getmastermode)
                guistrut 3
                guiradio "t:gui.master.mode.open" SM_MasterMode 0 [mastermode 0]
                guiradio "t:gui.master.mode.veto" SM_MasterMode 1 [mastermode 1]
                guiradio "t:gui.master.mode.lock" SM_MasterMode 2 [mastermode 2]
                guiradio "t:gui.master.mode.priv" SM_MasterMode 3 [mastermode 3]
            ]
            guistrut 1
            guialign 0 [//H
                guilist [//V
                    guistrut 18 1
                    guialign 0 [guicheckbox "show self" mastermenu_showself]
                    guistrut 1
                    if (ismaster $getclientnum) [
                        guialign 0 [guibutton (concatword "^f0" (gettrans "gui.master.unclaim")) [setmaster 0]]
                    ][
                        if (|| [listlen $SM_ListAdmn] [listlen $SM_ListAuth] [listlen $SM_ListMstr]) [
                            guialign 0 [guibutton (concatword "^f4" (gettrans "gui.master.claim")) [setmaster 1]]
                        ][
                            guialign 0 [guibutton (concatword "^f0" (gettrans "gui.master.claim")) [setmaster 1]]
                        ]
                    ]
                    guibar //-------------------------------------------------------------------------
                    guititle "master"
                    looplist iClnt $SM_ListMstr [
                        guialign 0 [guitext (concatword "^f0" (getclientname $iClnt) )]
                    ]
                ]
                guilist [//V
                    guistrut 18 1
                    guialign 0 [guicheckbox "show bots" mastermenu_showbots]
                    guistrut 1
                    guialign 0 [guicheckbox "separate spectators" mastermenu_splitplyrspec]
                    guibar //-------------------------------------------------------------------------
                    guititle "auth"
                    looplist iClnt $SM_ListAuth [
                        guialign 0 [guitext (concatword "^f5" (getclientname $iClnt) )]
                    ]
                ]
                guilist [//V
                    guistrut 18 1
                    guialign 0 [guicheckbox "confirm kick" mastermenu_confirmkick]
                    if (ismaster $getclientnum) [
                        guistrut 1
                        guialign 0 [guibutton "clear bans" clearbans]
                    ][
                        guistrut 2
                    ]
                    guibar //-------------------------------------------------------------------------
                    guititle "admin"
                    looplist iClnt $SM_ListAdmn [
                        guialign 0 [guitext (concatword "^f6" (getclientname $iClnt) )]
                    ]
                ]
            ]
        ]
    ]

    guitab (concatword "players [" $SM_NumOfPlyr "]")
        if (= $guitabnum 2) [SM_GenList $SM_ListPlyr]

    if $mastermenu_splitplyrspec [
        guitab (concatword "spectators [" $SM_NumOfSpec "]")
        if (= $guitabnum 3) [SM_GenList $SM_ListSpec]
    ]

    if $mastermenu_showbots [
        guitab (concatword "bots [" $SM_NumOfBots "]")
        if (= $guitabnum 4) [SM_GenList $SM_ListBots]
    ]

    ]]
] "t:gui.master" [SM_Update]


//#############################################################################################################################################
//#############################################################################################################################################
SM_Update = [
    if $getservdesc [
        SM_ServDesc = (concatword "^f7" $getservdesc)
        SM_ServAddr = (concatword $connectedip ":" $connectedport)
    ][
        if $connectedip [
        SM_ServDesc = "^f4~"
        SM_ServAddr = (concatword $connectedip ":" $connectedport)
        ][
            SM_ServDesc = "^f4Carmine Impact"
            SM_ServAddr = "^f4~"
        ]
    ]

    SM_ListClnt  = (listclients 1 0)
    SM_NumOfClnt = (listlen $SM_ListClnt)

    SM_ListAdmn = (listfilter i $SM_ListClnt [                                     isadmin $i  ])
    SM_ListAuth = (listfilter i $SM_ListClnt [&&                  (isauth $i)  (! (isadmin $i))])
    SM_ListMstr = (listfilter i $SM_ListClnt [&& (ismaster $i) (! (isauth $i)) (! (isadmin $i))])

    SM_ListClnt = (listclients $mastermenu_showself $mastermenu_showbots)

    if $mastermenu_splitplyrspec [
        SM_ListSpec = (listfilter i $SM_ListClnt [isspectator $i])
        SM_ListPlyr = (listfilter i $SM_ListClnt [&& (! (isspectator $i)) (! (isai $i))])
    ][
        SM_ListSpec = ""
        SM_ListPlyr = (listfilter i $SM_ListClnt [! (isai $i)])
    ]

    if $mastermenu_showbots [
        SM_ListBots = (listfilter i $SM_ListClnt [isai $i])
    ][
        SM_ListBots = ""
    ]

    SM_NumOfSpec = (listlen $SM_ListSpec)
    SM_NumOfPlyr = (listlen $SM_ListPlyr)
    SM_NumOfBots = (listlen $SM_ListBots)
]


//#############################################################################################################################################
//#############################################################################################################################################
// ARGS  1: [LIST]  List of client numbers.
SM_GenList = [
    guilist [// H
        //  CLIENT NUMBER:
        guilist [// V
            looplist iClnt $arg1 [
                guialign 1 [ guitext (concatword "[" $iClnt "]") ]
            ]
        ]
        //  NAME:
        guistrut 1
        guilist [// V
            looplist iClnt $arg1 [
                guitext (concatword (? (isadmin $iClnt) "^f6" (? (isauth $iClnt) "^f5" (? (ismaster $iClnt) "^f0"))) (getclientname $iClnt)) (getclienticon $iClnt)
            ]
        ]
        if (ismaster $getclientnum) [
            guispring
            //  BTN -- SWITCH TEAM:
            if (m_teammode $getmode) [
                guistrut 2
                guilist [// V
                    guistrut 9.09 1
                    looplist iClnt $arg1 [
                        if (isspectator $iClnt) [
                            guialign 0 [guitext "^f4~"]
                        ][
                            guibutton (concatword (? (=s (getclientteam $getclientnum)(getclientteam $iClnt)) "^f1" "^f3") "switchteam") [setteam @iClnt @(? (=s (getclientteam $iClnt) "red") "blue" "red")] 0
                        ]
                    ]
                ]
            ]
            guistrut 2
            //  BTN -- SPECTATOR:
            guilist [// V
                guistrut 5.9 1
                if (isai (at $arg1 0)) [
                    loop i $SM_NumOfBots [ guialign 0 [guitext "^f4~"] ]
                ][
                    looplist iClnt $arg1 [
                        SM_ClntIsSpec (isspectator $iClnt)
                        guicheckbox (concatword (? $SM_ClntIsSpec "^f8" "^f4") "spec") SM_ClntIsSpec 1 0 [spectator @(= (isspectator $iClnt) 0) @iClnt]
                    ]
                ]
            ]
            guistrut 2
            //  BTN -- MASTER/AUTH/ADMIN:
            guilist [// V
                guistrut 7.29 1
                if (isai (at $arg1 0)) [
                    loop i $SM_NumOfBots [ guialign 0 [guitext "^f4~"] ]
                ][
                    looplist iClnt $arg1 [
                        SM_ClntIsMstr (ismaster $iClnt)
                        guicheckbox (? (isadmin $iClnt) "^f6admin" (? (isauth $iClnt) "^f5auth" (? (ismaster $iClnt) "^f0master" "^f4master"))) SM_ClntIsMstr 1 0 [setmaster @(= (ismaster $iClnt) 0) @iClnt]
                    ]
                ]
            ]
            guistrut 2
            //  BTN -- KICK:
            guilist [// V
                guistrut 3.1 1
                if (isai (at $arg1 0)) [
                    loop i $SM_NumOfBots [ guialign 0 [guitext "^f4~"] ]
                ][
                    if $mastermenu_confirmkick [
                        looplist iClnt $arg1 [ guibutton (concatword "^f2" (gettrans "gui.master.cn.kick")) [SM_KickMe @iClnt; showgui "master_confirmkick"] 0 ]
                    ][
                        looplist iClnt $arg1 [ guibutton (concatword "^f2" (gettrans "gui.master.cn.kick")) [kick @iClnt] 0 ]
                    ]
                ]
            ]
        ]
    ]
]


//#############################################################################################################################################
newgui "master_confirmkick" [
    guistrut 28 1
    guialign 0 [guitext "^f7kick, are you sure?"]
    guialign 0 [guitext (concatword "^f6[" $SM_KickMe "] " (getclientname $SM_KickMe))]
    guistrut 1
    guialign 0 [
        guibutton (concatword "^f2" (gettrans "gui.master.cn.kick")) [kick $SM_KickMe; SM_KickMe -1; cleargui 1]
        guistrut 5
        guibutton "^f7cancel" [cleargui 1]
    ]
] 0

